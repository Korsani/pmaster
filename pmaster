#!/usr/bin/env perl
use warnings;
use strict;
use Data::Dumper;
use Term::ANSIColor qw(:constants colorstrip);
use File::Temp;
use Pod::Usage;
use Getopt::Long;
use File::Copy;
use DBI;
use Sys::Syslog qw( :standard :macros );
use Time::HiRes;
use constant VERSION	=> '0.14.4';

use constant DB_FILE	=> 'pmaster.db';
use constant DB_DIR		=> '/var/db';
use constant DB_VERS	=> 3;
use constant PM_OPTS	=> '-dbg';
use constant PID_FILE	=> '/var/run/pmaster.pid';

use vars qw( $HELP $EXISTS $DEBUG $CURRENT $PROGRESS $SHOWLAST $VERSION $WAIT $KILL $STATS $ONESHOT $DRYRUN $QUIET $INTERACT $FORCE );
use vars qw( $SHOWWORLD $ADDWORLD $RMWORLD );
use vars qw( $ADDEXCL $SHOWEXCL $RMEXCL );

die "Not on FreeBSD\n" unless ("$^O" =~ "freebsd");

$SIG{TERM}=\&end;

GetOptions(
	'add-excluded'	=> \$ADDEXCL,
	'add-world'		=> \$ADDWORLD,
	'current'		=> \$CURRENT,
	'debug'			=> \$DEBUG,
	'dryrun|n'		=> \$DRYRUN,
	'exists'		=> \$EXISTS,
	'force'			=> \$FORCE,
	'help:0'		=> \$HELP,
	'interactive'	=> \$INTERACT,
	'kill'			=> \$KILL,
	'show'			=> \$STATS,
	'show-last:1'	=> \$SHOWLAST,
	'ls-excluded'	=> \$SHOWEXCL,
	'ls-world'		=> \$SHOWWORLD,
	'oneshot|1'		=> \$ONESHOT,
	'progress'		=> \$PROGRESS,
	'quiet'			=> \$QUIET,
	'rm-excluded'	=> \$RMEXCL,
	'rm-world'		=> \$RMWORLD,
	'version'		=> \$VERSION,
	'wait=i'		=> \$WAIT,
);
if ($VERSION) {
	printf("%s\n",VERSION);
	exit 0;
}

if (defined $HELP) {
	pod2usage(-verbose => $HELP);
	exit;
}

my $DRIVER='SQLite';
my $DSN="DBI:$DRIVER:dbname=".DB_DIR.'/'.DB_FILE;
my %table=(
	'stats'=>'pmaster_stats',
	'config'=>'pmaster_config',
	'excludes'=>'pmaster_excludes'
);
# Sql statements to create table
my %create_table=(
	# 'time_user', 'time_sys', 'os' are unused
	$table{'stats'} => 'CREATE TABLE '.$table{'stats'}.' (id integer primary key autoincrement, timestamp timestamp DEFAULT CURRENT_TIMESTAMP, origin text, time_real float, time_user float, time_sys float,portmaster_options text, mac text,os text, kernel_vers text, arch text, cpu_freq integer, memory integer, ccache bool, log_size_lines integer);',
	$table{'config'} => 'CREATE TABLE '.$table{'config'}.' (key text type uniq, value text);'
);
# Sql statements to update database
my %update_table=(
	2=>'alter table '.$table{'stats'}.' add world boolean default true',
	3=>'create table '.$table{'excludes'}.' (origin text)'
);

our %env;

sub disp {
	my %msg=%{shift()};
	$msg{'prefix'}=1 if (not defined $msg{'prefix'});
	if ($msg{'prefix'} and $DEBUG) {
		my $sub;
		if (caller(1)) {
			$sub=(split(/::/,(caller(1))[3]))[1];
		} else {
			$sub='main';
		}
		my $line=(caller(0))[2];
		$msg{'prefix'}=sprintf('['.YELLOW.'pmaster'.RESET.']('.CYAN.'%s:%s'.RESET.') ',$sub,$line);
	} else {
		$msg{'prefix'}=sprintf('['.YELLOW.'pmaster'.RESET.'] ');
	}
	my $msg=sprintf('%s'.$msg{'msg'}."\n",$msg{'prefix'},@{$msg{'params'}});
	if (-t STDOUT) {
		print($msg);
	} else {
		print(colorstrip($msg));
	}
}
sub check_db_tbl {
	my $dbh=shift;
	# Let's check if tables exists
	foreach my $table (values %table) {
		if (! $dbh->selectall_array('SELECT name FROM sqlite_master WHERE type="table" AND name="'.$table.'";')) {
			my @res=$dbh->selectall_array($create_table{$table});
			disp({msg=>"Table %s created",params=>[$table]});
		} elsif ($DEBUG) {
			disp({msg=>"Table %s ok",params=>[$table]});
		}
	}
}
sub check_db_vers {
	my $dbh=shift;
	my $sql=sprintf('select value from %s where key="%s";',$table{'config'},'db_vers');
	my $db_vers=($dbh->selectall_array($sql))[0][0];
	# None? Maybe db is juste created
	if($db_vers == 0) {;
		$sql=sprintf('insert into %s (key,value) values ("db_vers",%i);',$table{'config'},DB_VERS);
		$dbh->selectall_array($sql);
		disp({msg=>'Db version set to %i',params=>[DB_VERS]});
	} elsif ($db_vers < DB_VERS) {		# Time to upgrade!
		foreach my $i ($db_vers+1..DB_VERS) {
			disp({msg=>'Updating %d to %d',params=>[$db_vers,$i]}) if ($DEBUG);
			$dbh->selectall_array($update_table{$i});
			$sql=sprintf('update %s set value=%i where key="db_vers";',$table{'config'},$i);
			$dbh->selectall_array($sql);
			disp({msg=>'Database schema version set to %d',params=>[$i]}) if ($DEBUG);
		}
	} else {
		disp({msg=>'Database schema version: %d',params=>[$db_vers]}) if ($DEBUG);
	}
}
sub check_db_path {
	for my $dir ($ENV{'HOME'}) {
		if ( -e $dir.'/'.DB_FILE) {
			disp({msg=>'Moving %s from %s to %s',params=>[DB_FILE,$dir,DB_DIR]});
			move($dir.'/'.DB_FILE,DB_DIR);
		}
	}
}
sub check_db {
	my $dbh=shift;
	check_db_path($dbh);
	check_db_tbl($dbh);
	check_db_vers($dbh);
}
# Function to get ports(7) variable's values
# For that, I ask a Makefile to display me vars and values
sub get_make_var {
	my $var=shift;
	my $uvar=uc($var);
	my $fh=File::Temp->new();
	print $fh "CATEGORIES=devel\n.include <bsd.port.mk>\ninfo:\n\t\@echo ".$uvar."=\$(".$uvar.")";
	open(M,'make -f '.$fh->filename.' info|') or die "$!";
	while (my $line=<M>) {
		if ($line=~/^$uvar=(.*)/) {
			return $1;
		}
	}
}
sub get_make_vars {
	my $dbh=shift;
	my @vars=('PORTSDIR','WRKDIRPREFIX','WITH_CCACHE_BUILD');
	foreach my $var (@vars) {
		my @res=$dbh->selectall_array('select value from '.$table{'config'}.' where key="'.uc($var).'";');
		if(scalar @res == 0) {
			disp({msg=>'No var %s found in config. Getting it',params=>[$var]}) if ($DEBUG);
			my $value=get_make_var($var);
			$dbh->selectall_array('insert into '.$table{'config'}.' (key,value) values ("'.$var.'","'.$value.'");');
			$env{$var}=$value;
		} else {
			my $v=@{ shift @res }[0];
			$env{$var}=$v;
			disp({msg=>'%s:%s',params=>[uc($var),$v]}) if ($DEBUG);
		}
	}
}
# Get average compile time of a port
sub get_port_stats {
	my $dbh=shift;
	my $o=shift;
	my $sql='select avg(time_real) from '.$table{'stats'}.' where origin="'.$o.'"';
	my @res=$dbh->selectall_array($sql);
	if (scalar @res == 0) {
		return undef;
	} else {
		return shift @{ shift @res };
	}
}
# Return given field
sub get_db_infos {
	my $dbh=shift;
	my $o=shift;
	my $field=shift;
	my $sql='select id,'.$field.' from '.$table{'stats'}.' where origin="'.$o.'" order by id';
	my @res=$dbh->selectall_array($sql);
	if (scalar @res == 0) {
		return undef;
	} else {
		return @res;
	}
}
# Translate arbitrary string to a port name
sub resolve_origin {
	my $dbh=shift;
	my @origins;
	my $kvers=`uname -K`;
	chomp $kvers;
	$kvers=~s/^(..).*/$1/;
	while (my $pkg=shift) {
		disp({msg=>'Resolving '.BRIGHT_WHITE.'%s'.RESET,params=>[$pkg]}) if ($DEBUG);
		my @ports=`pkg info -o $pkg 2>/dev/null | awk '{print \$2}'`;
		chomp @ports;
		if ( scalar @ports == 0) {
			disp({msg=>'Not found by pkg(1). Searching in INDEX'}) if ($DEBUG);
			# So it's a package I don't know about. Let's search in INDEX
			if (open(I,"<$env{PORTSDIR}/INDEX-$kvers")) {
				while (my $line = <I>) {
					if ($line =~ /$env{PORTSDIR}\/[\w\-]*\/?$pkg\|/i) {
						my $port=((split(/\|/,$line))[1]);
						$port=~s/$env{PORTSDIR}\///;
						push @ports,$port;
					}
				}
				close(I);
			} else {
				disp({msg=>'Problem with %s: %s',params=>["$env{PORTSDIR}/INDEX-$kvers",$!]});
			}
		}
		if (scalar @ports == 0) {
			disp({msg=>'No origin found for '.BRIGHT_WHITE.'%s'.RESET,params=>[$pkg]});
		} elsif (scalar @ports == 1) {
			disp({msg=>'%s ~> %s',params=>[$pkg,$ports[0]]}) if ($DEBUG);
			push @origins,($ports[0]);
		} else {
			disp({msg=>'More than one origin found for %s',params=>[$pkg]}) if ($DEBUG);
		}
	}
	return @origins;
}
sub show_stats {
	my $dbh=shift;
	my @pkgs=@_;
	# Display stats of given ports
	foreach my $port (@pkgs) {
		$port=~s!$env{'PORTSDIR'}/(.*)!$1!;
		my $sql='select timestamp,time_real from '.$table{'stats'}.' where origin="'.$port.'"';
		my @res=$dbh->selectall_array($sql);
		if (scalar @res > 0) {	
			my @times;
			foreach my $e (@res) {
				push @times,(join(' => ',@{ $e }));
			}
			disp({prefix=>0,msg=>YELLOW.'%s'.RESET.":\n\t".CYAN."Average duration:".RESET." %0is\n\t".CYAN."Series: \n".RESET."%s",params=>[$port,get_port_stats($dbh,$port),join("\n",@times)]});
		}
	}
}
# Check wether a port is in the exclusion list
sub is_excluded {
	my $dbh=shift;
	my $origin=shift;
	my $sql=sprintf('select origin from %s where origin="%s"',$table{'excludes'},$origin);
	my @res=($dbh->selectall_array($sql))[0];
	return scalar @res;
}

sub get_port_deps {
	my $dbh=shift;
	my $origin=shift;
	disp({msg=>'Gathering dependencies of '.BRIGHT_WHITE.'%s'.RESET,params=>[$origin]});
	my %dependencies;	# origin => +/~ (add / update package)
	open(W,"portmaster --show-work $origin |") or die "$!";
	while (my $l=<W>) {
		# ===>>> NOT INSTALLED            x11/libXau
		if ($l=~/.*NOT INSTALLED.*/) {
			$l=(split /\s+/,$l)[3];
			disp({msg=>BRIGHT_WHITE.'%s'.RESET.' not installed, adding',params=>[$l]}) if ($DEBUG);
			chomp($l);
			$dependencies{$l}='+';
		} elsif ($l=~/===>>> Installed.*/) {
			# ===>>> Installed ftp/curl
			$l=(split /\s+/,$l)[2];
			disp({msg=>BRIGHT_WHITE.'%s'.RESET.' installed, checking updates',params=>[$l]}) if ($DEBUG);
			my $pkg_upd=`pkg version -L= -oO $l`;
			chomp($pkg_upd);
			if ($pkg_upd) {
				$pkg_upd=~s/\s+.*//;
				if (is_excluded($dbh,$pkg_upd)) {
					disp({msg=>BRIGHT_WHITE.'%s'.RESET.' is installed and to be updated BUT in exclusion list',params=>[$pkg_upd]}) if ($DEBUG);
				} else {
					disp({msg=>BRIGHT_WHITE.'%s'.RESET.' is to be updated',params=>[$pkg_upd]}) if ($DEBUG);
					$dependencies{$pkg_upd}='~' unless (is_excluded($dbh,$pkg_upd));
				}
			}
		}
	}
	close(W);
	if (scalar keys %dependencies gt 0) {
		disp({msg=>BRIGHT_YELLOW.'%d'.RESET.' found',params=>[scalar keys %dependencies]});
	} else {
		disp({msg=>'None found',params=>[]});
	}
	return(%dependencies);
}
# Run compilation of a port
sub build_port {
	my $dbh=shift;
	my $port=shift;
	my $world=shift;
	my %built_ports;	# Keep track of built ports, particularly those that ports depend on
	my %dependencies=get_port_deps($dbh,$port);
	if (scalar keys %dependencies gt 0) {
		map {disp({msg=>BRIGHT_BLUE.$dependencies{$_}.' '.BRIGHT_WHITE.'%s'.RESET,params=>[$_]})} keys %dependencies;
		foreach my $port (keys %dependencies) {
			# Build each dependency
			%built_ports=(%built_ports,build_port($dbh,$port,0));
		}
	}
	my @a=get_db_infos($dbh,$port,'log_size_lines');
	# Returns [ [ id, field ] [ ... ] ]
	# And I want the field of the last element
	my $lines=@{ $a[-1]}[1];
	my $time=get_port_stats($dbh,$port)||'?';
	disp({msg=>'Building '.BRIGHT_WHITE.'%s'.RESET.' (~'.BRIGHT_WHITE.'%s'.RESET.'s)',params=>[$port,$time]});
	if ($DRYRUN) {
		disp({msg=>'Dry run asked'});
		return(());
	}
	if ($INTERACT) {
		disp({msg=>'Ready? (Yn)'});
		my $r=<STDIN>;
		chomp $r;
		return(()) if ($r=~/^n.*/i);
	}	
	# If line size is available and progress is asked
	my $start=Time::HiRes::time();
	my $ec=0;
	if ($lines and $PROGRESS) {
		my $cmd='portmaster '.PM_OPTS.' '.$port." | pv -l -s $lines -pet >/dev/null";
		$ec=system($cmd);
	} else {
		$lines=0;
		open(P,'portmaster --no-confirm '.PM_OPTS.' '.$port.'|') or $ec=1;
		while(<P>) {
			$lines++;
			print;
		}
		close(P);
		$ec=$?;
	}
	my $end=Time::HiRes::time();
	parse_stats($dbh,$port,($end-$start));
	# If everything went fine...
	if ($ec == 0) {
		store_stats($dbh,$port,($end-$start),$lines,$world);
		disp({msg=>BRIGHT_WHITE.'%s'.RESET.' added to '.BRIGHT_BLUE.'world'.RESET,params=>[$port]}) if ($world);
	} else {
		disp({msg=>'Something failed with %s building: portmaster exits with %i',params=>[$port,$ec/256]});
	}
	$built_ports{$port}={'world'=>$world,'return_code'=>$ec/256,'time_taken'=>$end-$start};
	return(%built_ports);
}
sub parse_stats {
	my $dbh=shift; my $o=shift; my $real=shift;
	my $time=get_port_stats($dbh,$o)||1;
	disp({msg=>'%s: '.BRIGHT_WHITE.'%is'.RESET.'? %is! d:%is s:%-.02f%%',params=>[$o,$time,$real,$real-$time,$real*100/$time]});
}

# Store stats to database
sub store_stats {
	my $dbh=shift; my $o=shift; my $real=shift; my $lines=shift; my $world=shift;
	my $mac=`uname -m|xargs echo -n`;		# Machine type (amd64, arm64, ...)
	my $kvers=`uname -K|xargs`;	# Kerner version (1201000, ...)
	my $arch=`uname -p|xargs`;	# Arch (amd64, aarch64,...
	my $mem=`sysctl -n hw.physmem|xargs`;		# Physical amount of memory
	my $cfreq=`sysctl -n dev.cpu.0.freq|xargs`;	# Cpu freq. May vary by sake of powerd(8)/powerdxx(8)
	my $sql=sprintf("insert into %s (origin,time_real,portmaster_options,mac,kernel_vers,arch,cpu_freq,memory,ccache,log_size_lines,world) values ('%s',%i,'%s','%s','%s','%s',%i,%i,%i,%i,%d);select last_insert_rowid();",$table{'stats'},$o,	$real,PM_OPTS, `uname -m| xargs echo -n`,`uname -K|xargs echo -n`,`uname -p | xargs echo -n`,`sysctl -n dev.cpu.0.freq| xargs echo -n`, `sysctl -n hw.physmem| xargs echo -n`, $env{'WITH_CCACHE_BUILD'} eq 'yes' ? 1 : 0, $lines,$world);
	$dbh->selectall_array($sql);
}
sub loop_on_me {
	my $dbh=shift;
	my $a=shift;
	if ( -e PID_FILE) {
		open(P,"cat ".PID_FILE.'|') or die "$!"; 
		my $pid=<P>;
		disp({msg=>'An instance of me seems to exists under pid %d',params=>[$pid]});
		close(P);
		return;
	} else {
		open(P,">".PID_FILE) or die "$!";
		print P $$;
		close(P);
	}
	while (1) {
		if ($a) {
			foreach my $k (keys %{$a}) {
				my $bar='[          ]';
				my $progress=int($a->{$k}{'progress'}/10);
				if ($progress <= 10) {
					substr($bar,1,$progress,'#' x $progress);
				} else {
					substr($bar,1,9,'#' x 9);
					substr($bar,10,1,'+');
				}
				my $mess=sprintf('%s %ds %s',$a->{$k}{'port'},time()-$a->{$k}{'start_time'},$bar);
				syslog(LOG_INFO,$mess);
			}
		}
		$a=get_cur_work($dbh);
		sleep($WAIT);
	}
}
sub get_cur_work {
	my $dbh=shift,
	my @list=glob ( "$env{'WRKDIRPREFIX'}$env{'PORTSDIR'}/*/*/work*" );
	my %res;
	if (scalar @list == 0) {
		disp({msg=>'No ongoing work found',params=>[]}) unless ($WAIT);
		return \%res;
	}
	foreach my $w (@list) {
		my $port=$w;
		$port=~s!$env{'WRKDIRPREFIX'}$env{'PORTSDIR'}/(.*)/work.*!$1!;
		disp({msg=>'Dealing with '.BRIGHT_WHITE.'%s'.RESET.' ~ %s',params=>[$w,$port]}) if ($DEBUG);
		# This is the best way I found to detect ongoing portmaster:
		# There should have more at least one process with `make` and $WRKDIRPREFIX$PORTSDIR<port>/work somewhere...
		my $npid=`ps -a | grep -v grep | grep '$w' | awk '{print \$1}'| wc -l|xargs`;
		chomp $npid;
		if ($npid > 0) {
			exit 0 if ($EXISTS);
			disp({msg=>'Compilation in '.BRIGHT_WHITE.'%s'.RESET.' is ongoing',params=>[$w]}) if ($DEBUG);
			# When base dir has been created?
			my @stats=stat($env{'WRKDIRPREFIX'}.$env{'PORTSDIR'}.'/'.$port);
			my $eta='?';my $progress='?';
			if (my $time = get_port_stats($dbh,$port)) {
				$eta=sprintf('%0i',$time-(Time::HiRes::time()-$stats[10]));
				$progress=sprintf('%2i',($time-$eta)/$time*100);
			}
			$res{$port}={port=>$port,start_time=>$stats[10],eta=>$eta,progress=>$progress};
		} else {
			disp({msg=>'No process seems to handle compilation in '.BRIGHT_WHITE.'%s'.RESET,params=>[$w]}) if ($DEBUG);
		}
	}
	if (scalar keys %res == 0) {
		disp({msg=>BRIGHT_WHITE.'%d stall work(s) found:'.RESET,params=>[scalar @list]}) if ($DEBUG or ! $WAIT);
		foreach my $w (@list) {
			disp({msg=>'> %s',params=>[$w]}) if ($DEBUG or ! $WAIT);
		}
	}
	return \%res;
}
sub list_world {
	my $dbh=shift;
	my $sql=sprintf('select origin from %s where world = 1 group by origin order by origin',$table{stats});
	my @res=$dbh->selectall_array($sql);
	return \@res;
}

my $dbh=DBI->connect($DSN,'','',{RaiseError => 1}) or die $DBI::errstr;
disp({msg=>"Database:%s",params=>[$DSN]}) if ($DEBUG);

check_db($dbh);

# == let's run

my @pkgs;
if (scalar @ARGV ne 0) {
	@pkgs=@ARGV;
}
get_make_vars($dbh);
if ($SHOWLAST) {
	my $sql=sprintf('select origin from %s group by origin order by id desc limit %d;',$table{'stats'},$SHOWLAST);
	my @e=$dbh->selectall_array($sql);
	foreach my $o (@e) {
		my $p=shift @{$o};
		push @pkgs,($p);
	}
	$STATS=1;
} else {
	# As I assume that pkgs taken from db are already clean...
	@pkgs=resolve_origin($dbh,@pkgs);
}

# Just show stats of given ports
if ($STATS) {	# show stats about a package
	if (scalar @pkgs > 0) {
		show_stats($dbh,@pkgs);
		exit 0;
	} else {
		disp({msg=>'Nothing to show'});
		exit 0;
	}
} elsif ($CURRENT) {					# Show what portmaster is currently doing
	openlog('pmaster','ndelay,pid','local0');
	my $a=get_cur_work($dbh);
	if ($a) {
		my $i=1;
		foreach my $k (keys %{$a}) {
			disp({prefix=>0,msg=>WHITE.'<'.$i++.'>'.RESET.' '.YELLOW.'%s'.RESET.":\n\t".CYAN.'Started'.RESET.": %s\n\t".CYAN.'Elapsed'.RESET.": %0is\n\t".CYAN.'ETA'.RESET.": %s\n\t".CYAN.'Progress'.RESET.": %2s%%",params=>[$a->{$k}{'port'},scalar localtime $a->{$k}{'start_time'},Time::HiRes::time()-$a->{$k}{'start_time'},($a->{$k}{'eta'} eq '?'?'?' : ($a->{$k}{'eta'}.'s ('.scalar localtime (time()+$a->{$k}{'eta'})).')'),$a->{$k}{'progress'}]});
		}
	}
} elsif ($WAIT) {
	my $a=get_cur_work($dbh);
	openlog('pmaster','ndelay,pid','local0');
	exit if (my $pid = fork);
	disp({prefix=>0,msg=>'pmaster %s backgrounded',params=>[VERSION]});
	if ($a) {
		loop_on_me($dbh,$a);
	} else {
		loop_on_me($dbh,{port=>'(idle)',progress=>'?',eta=>0});
	}
} elsif ($KILL) {
	if ( ! -s PID_FILE) {
		disp({msg=>'No pid file found',params=>[]});
	} else {
		open(P,'<'.PID_FILE);
		my $pid=<P>;
		disp({msg=>'Pid found with pid %s',params=>[$pid]}) if ($DEBUG);
		close(P);
		my $process=`ps $pid>/dev/null`;
		if ($? eq 0) {
			kill 'TERM',$pid;
			disp({msg=>'Process %s killed',params=>[$pid]});
		}
	}
	
} elsif ($ADDEXCL) {
	my $sql='insert into %s (origin) values ("%s")';
	foreach my $port (@pkgs) {
		disp({msg=>'Puting '.BRIGHT_WHITE.'%s'.RESET.' as excluded',params=>[$port]});
		$sql=sprintf($sql,$table{'excludes'},$port);
		$dbh->selectall_array($sql);
	}
} elsif ($RMEXCL) {
	my $sql='delete from %s where origin="%s"';
	foreach my $port (@pkgs) {
		disp({msg=>'Removing '.BRIGHT_WHITE.'%s'.RESET.' from exclusion list',params=>[$port]});
		$sql=sprintf($sql,$table{'excludes'},$port);
		$dbh->selectall_array($sql);
	}
} elsif ($SHOWEXCL) {
	my $sql='select origin from %s group by origin order by origin';
	$sql=sprintf($sql,$table{'excludes'});
	my @res=$dbh->selectall_array($sql);
	map {my $a=$_;print @{$a}[0],"\n"} @res;
} elsif ($SHOWWORLD) {
	my $res=list_world($dbh);
	if (scalar @{$res} > 0) {
		foreach my $aport (@{$res}) {
			my $port=shift @{$aport};
			system("pkg info -e $port");
			# Display only installed ports
			disp({prefix=>0,msg=>'%s',params=>[$port]}) if ($?==0);
		}
	} 
} elsif ($RMWORLD) {
	foreach my $port (@pkgs) {
		my $sql='update %s set world=false where origin="%s"';
		disp({msg=>'Puting '.BRIGHT_WHITE.'%s'.RESET.' out of '.BRIGHT_BLUE.'world'.RESET,params=>[$port]});
		$sql=sprintf($sql,$table{'stats'},$port);
		$dbh->selectall_array($sql);
	}
} elsif ($ADDWORLD) {
	my $sql='update %s set world=true where origin="%s"';
	foreach my $port (@pkgs) {
		disp({msg=>'Puting '.BRIGHT_WHITE.'%s'.RESET.' in '.BRIGHT_BLUE.'world'.RESET,params=>[$port]});
		$sql=sprintf($sql,$table{'stats'},$port);
		$dbh->selectall_array($sql);
	}
} else {		# Compile ports
	my %built_ports;
	if (scalar @pkgs == 0) {
		disp({msg=>'Nothing to build'});
	}
	foreach my $port (@pkgs) {
		if (is_excluded($dbh,$port) and ! $FORCE ) {
			disp({msg=>BRIGHT_WHITE.'%s'.RESET.' is in the excluded list',params=>[$port]});
		} elsif (defined $built_ports{$port}) {
			disp({msg=>BRIGHT_WHITE.'%s'.RESET.' already built in this session',params=>[$port]});
		} else {
			%built_ports=(%built_ports,build_port($dbh,$port,$ONESHOT?0:1));
		}
	}
	if (scalar keys %built_ports > 0) {
		disp({msg=>'Summary:'});
		map {disp({msg=>'- %s'.BRIGHT_WHITE.'%s'.RESET.':%s (in %is)',params=>[$built_ports{$_}{'world'}?BRIGHT_BLUE.'w'.RESET.':':'  ',$_,$built_ports{$_}{'return_code'}==0?BRIGHT_GREEN.'success'.RESET:BRIGHT_RED.'failure'.RESET,$built_ports{$_}{'time_taken'}]});} keys %built_ports;
	}
}
sub end {
	open(P,"cat ".PID_FILE.'|') or return; 
	my $pid=<P>;
	if ($pid eq $$) {	# My pid, so I can remove it
		unlink PID_FILE;
	}
	close(P);
	exit;
}

=pod

=head1 NAME

	portmaster(8) companion

=head1 SYNOPSIS

	pmaster [ --debug ] [ --progress ] [ --oneshot|-1 ] [ --interactive ] [ --dryrun ] <port>
	pmaster [ --debug ] [ --exists ] --current
	pmaster [ --debug ] --show-last [ <n> ]
	pmaster [ --debug ] --show <port>
	pmaster [ --debug ] --wait <n>
	pmaster [ --debug ] --kill
	pmaster [ --debug ] --ls-world | --add-world | --rm-world [ <port> ]
	pmaster [ --debug ] --ls-excluded | --add-excluded | --rm-excluded [ <port> ]
	pmaster --help [ <n> ]

=head1 DESCRIPTION

Wrapper for portmaster(8) to add some functionalities, such as:

	- Currently port building,
	- Estimated time for a build,
	- A `world` list, as Gentoo has,
	- An exclusion list

It uses a sqlite3(1) database.

=head1 OPTIONS

=over

=item <port>

Build this port with portmaster(8).

=item --current

Show currently building port.

Return immediately with true if a compilation is ongoing and B<--exists> is specified.

=item --debug

Show debugging informations.

=item --dryrun

Does everything except building the port and writing into db.

=item --*-excluded (ls, add, rm)

List / add / remove ports to the excluded list.

Ports added to that list will not be updated or built.

=item --force

Force building even if port is in the exclusion list.

=item --help

Help?

Specify up to three times for a more detailed help.

=item --interactive

Ask before building port.

=item --kill

Kill a running instance of pmaster(1)

=item --oneshot

Do not flag port as part of `world` list.

=item --show

Show statistics about port(s).

=item --show-last [ <n> ]

Show last [n] built port.


=item --wait

Go to background and wait forever, logging to syslog every B<n> seconds what's going on.

syslog message looks like:

    dumb 100s [#######   ]

First is the port name. Second is time it's compiling for. Third is a progress bar.

If compiling time is greater that forseen time, syslog message will look like:

    dumb 990s [#########+]

=item --*-world (--ls-world, --add-world, --rm-world)

Show/add/remove ports from `world` list.

In Gentoo, the `world` file is a list of packages you intended to built and that should be auto removed. Ports built by pmaster(1) are flagged `world` by default.

Please note it does not keep track of package installer via pkg(8) or other package manager.

=back

=head1 FILES

=head1 NOTES

=head1 BUGS

=cut
